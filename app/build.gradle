/* SPDX-License-Identifier: GPL-3.0-or-later
 * SPDX-FileCopyrightText: 2016-2025 Soren Stoutner <soren@stoutner.com>
 *
 * This file is part of Privacy Browser Android <https://www.stoutner.com/privacy-browser-android/>.
 *
 * This program is free software: you can redistribute it and/or modify it under
 * the terms of the GNU General Public License as published by the Free Software
 * Foundation, either version 3 of the License, or (at your option) any later
 * version.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
 * details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 35

    defaultConfig {
        minSdk 28
        targetSdk 35
        versionCode 1
        versionName "1.0"
        resourceConfigurations += ['en', 'de', 'es', 'fr', 'it', 'pt-rBR', 'ru', 'tr', 'zh-rCN']
    }

    // Specify the compile Java version.  This must match the Kotlin JVM target.  For some reason the system was not setting this correctly.  These can probably be removed in the future.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    // Specify the Kotlin JVM target.  This must match the compile Java version.  For some reason the system was not setting this correctly.  These can probably be removed in the future.
    kotlinOptions {
        jvmTarget = 17
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        // Generate BuildConfig so it can be accessed from within the app.
        buildConfig = true
    }

    // Gradle requires a `flavorDimension`, but it isn't used for anything in Privacy Browser.
    flavorDimensions = ['basic']

    productFlavors {
        standard {
            applicationId "com.audeon.browser.standard"
            dimension "basic"
        }

        alt {
            applicationId "com.audeon.browser.alt"
            dimension "basic"
        }
    }

    namespace 'com.audeon.browser'
}

dependencies {
    // Include the following AndroidX libraries.
    implementation "androidx.activity:activity-ktx:1.10.1"
    implementation 'androidx.arch.core:core-common:2.2.0'
    implementation 'androidx.arch.core:core-runtime:2.2.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.3.0'
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.drawerlayout:drawerlayout:1.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.9.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.viewpager:viewpager:1.1.0'
    implementation 'androidx.webkit:webkit:1.14.0'

    // Include the Kotlin standard library.  This should be the same version number listed in project build.gradle.
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.1.20'

    // Include the Google material library.
    implementation 'com.google.android.material:material:1.13.0-alpha14'
}
